/**
 *	Base for a standard game mode
 */

#RequireContext CTmMode
#Const ModeBaseVersion		"2013-03-22"
#Const ModeBaseScriptName	"ModeBase.Script.txt"

#Include "Libs/Nadeo/Mode.Script.txt" as Mode
#Include "Libs/Nadeo/TrackMania/TM.Script.txt" as TM

#Setting S_UseScriptCallbacks True as "<hidden>"	///< Turn on/off the script callbacks, usefull for server manager

#Const C_PlayersPresentationTime	4000	///< Duration of the player presentation sequence (default: 4000)

/* -------------------------------------- */
// Extends
/* -------------------------------------- */
***LogVersion***
***
MB_LogVersion(ModeBaseScriptName, ModeBaseVersion);
MB_LogVersion(Mode::GetScriptName(), Mode::GetScriptVersion());
***

***MapIntro***
***
if (MB_UseLogging) MB_Log("MapIntro");
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Intro;
wait(UIManager.UIAll.UISequenceIsCompleted);
***

***MapOutro***
***
if (MB_UseLogging) MB_Log("MapOutro");
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Outro;
wait(UIManager.UIAll.UISequenceIsCompleted);
***

/* -------------------------------------- */
// Globales
/* -------------------------------------- */
// Number of time the mode have gone through a section
declare Integer MB_SectionMatchNb;
declare Integer MB_SectionMapNb;
declare Integer MB_SectionSubmatchNb;
declare Integer MB_SectionRoundNb;
declare Integer MB_SectionTurnNb;
// Section switch
declare Boolean MB_StopServer;
declare Boolean MB_StopMatch;
declare Boolean MB_StopMap;
declare Boolean MB_StopSubmatch;
declare Boolean MB_StopRound;
declare Boolean MB_StopTurn;
// Options for XmlRpc
declare Boolean MB_UseScriptCallbacks;

/* -------------------------------------- */
// Functions
/* -------------------------------------- */
/* -------------------------------------- */
/** Log the version of a script
 *
 *	@param	_Name		Name of the script
 *	@param	_Version	Version of the script
 */
Void MB_LogVersion(Text _Name, Text _Version) {
	log(Now^"> Script: "^_Name^" | Version: "^_Version);
}

/* -------------------------------------- */
/** Custom log function
 *
 *	@param	_Message	The message to log
 */
Void MB_Log(Text _Message) {
	log(Now^"> "^_Message);
}

/* -------------------------------------- */
/** Custom sleep function
 *
 *	@param	_Duration	The time to spend sleeping in ms
 */
Void MB_Sleep(Integer _Duration) {
	declare End = Now + _Duration;
	while(Now < End) {
		yield;
		+++SleepLoop+++
	}
}

/* -------------------------------------- */
/// Do the player presentation sequence (aka versus screen)
Void MB_PlayersPresentationSequence() {
	declare Start = Now;
	sleep(500);
	UIManager.UIAll.UISequence = CUIConfig::EUISequence::PlayersPresentation;
	while (Now < Start + C_PlayersPresentationTime) {
		yield;
	}
}

/* -------------------------------------- */
// Main // Server start
/* -------------------------------------- */
main() {
	// Display the version of each script used in the game mode
	+++LogVersion+++
	// Server initialization
	// Options
	declare MB_UseLogging	= False;
	declare MB_UseIntro		= True;
	declare MB_UseOutro		= True;
	// Determine wich section will be used
	declare MB_UseSectionSubmatch	= False;	///< Use the submatch section
	declare MB_UseSectionRound		= False;	///< Use the round section
	declare MB_UseSectionTurn		= False;	///< Use the turn section
	// Options for Clublinks
	declare MB_NeutralEmblemUrl			= "";
	declare MB_CustomClublinkLayerUrl	= "";
	declare MB_UsePlayerClublinks		= False;
	declare MB_ForceClansFromClublinks 	= False;		///< only active if MB_UsePlayerClublinks
	
	MB_StopServer = False;
	MB_SectionMatchNb = 0;
	
	if (MB_UseLogging) MB_Log("StartServer");

	+++InitServer+++
	+++StartServer+++

/* -------------------------------------- */
// Match sequence start
/* -------------------------------------- */
	while (
		!ServerShutdownRequested
		&& !MB_StopServer
	) {
		// Match initialization
		MB_SectionMatchNb	+= 1;
		MB_SectionMapNb		= 0;
		MB_StopMatch		= False;
		
		if (MB_UseLogging) MB_Log("StartMatch");
		
		+++InitMatch+++
		+++StartMatch+++

/* -------------------------------------- */
// Map sequence start
/* -------------------------------------- */
		while (
			!ServerShutdownRequested
			&& !MB_StopServer
			&& !MB_StopMatch
		) {
			// Map initialization
			MB_SectionMapNb += 1;
			MB_SectionSubmatchNb = 0;
			MB_StopMap = False;
			MatchEndRequested = False;
			
			Mode::LoadMap();
			
			if (MB_UseLogging) MB_Log("StartMap");
	
			+++InitMap+++

			Mode::Synchro_DoBarrier();
			
			// Play mediatracker intro
			if (MB_UseIntro) {
				---MapIntro---
			}
			
			+++StartMap+++
		
/* -------------------------------------- */
// Submatch sequence start (King of the Map style)
/* -------------------------------------- */
			while (
				!ServerShutdownRequested
				&& !MB_StopServer
				&& !MatchEndRequested
				&& !MB_StopMatch
				&& !MB_StopMap
			) {
				// Submatch initialization
				+++InitSubmatch+++
				MB_StopSubmatch = False;
				if (MB_UseSectionSubmatch) {
					MB_SectionSubmatchNb += 1;
					if (MB_UseLogging) MB_Log("StartSubmatch");
					+++StartSubmatch+++
				}
				MB_SectionRoundNb = 0;
				
/* -------------------------------------- */
// Round sequence start
/* -------------------------------------- */				
				while (!ServerShutdownRequested
					&& !MB_StopServer
					&& !MatchEndRequested
					&& !MB_StopMatch
					&& !MB_StopMap
					&& !MB_StopSubmatch
				) {
					// Round initialization
					+++InitRound+++
					MB_StopRound = False;
					if (MB_UseSectionRound) {
						MB_SectionRoundNb += 1;
						if (MB_UseLogging) MB_Log("StartRound");
						+++StartRound+++
					}
					MB_SectionTurnNb = 0;
					
/* -------------------------------------- */
// Turn begin
/* -------------------------------------- */
					while (!ServerShutdownRequested
						&& !MB_StopServer
						&& !MatchEndRequested
						&& !MB_StopMatch
						&& !MB_StopMap
						&& !MB_StopSubmatch
						&& !MB_StopRound
					) {
						// Turn initialization
						+++InitTurn+++
						MB_StopTurn = False;
						/* -------------------------------------- */
						// Initialize players and spectators
						declare MB_AllPlayers = CTmPlayer[];
						foreach (Player in Players) MB_AllPlayers.add(Player);
						foreach (Player in MB_AllPlayers) {
							declare MB_NewPlayer for Player = True;
							declare MB_NewSpectator for Player = True;
							MB_NewPlayer = True;
							MB_NewSpectator = True;
						}
						
						if (MB_UseSectionTurn) {
							MB_SectionTurnNb += 1;
							if (MB_UseLogging) MB_Log("StartTurn");
							+++StartTurn+++
						}
						
/* -------------------------------------- */
// Play loop
/* -------------------------------------- */
						while (!ServerShutdownRequested
							&& !MB_StopServer
							&& !MatchEndRequested
							&& !MB_StopMatch
							&& !MB_StopMap
							&& !MB_StopSubmatch
							&& !MB_StopRound
							&& !MB_StopTurn
						) {
							yield;
							
							/* -------------------------------------- */
							// Create a custom event when a player is added to the Players array
							foreach (Player in Players) {
								declare MB_NewPlayer for Player = True;
								declare MB_NewSpectator for Player = True;
								
								if (MB_NewPlayer) {
									if (MB_UseLogging) MB_Log("New player > Login: "^Player.Login);
									MB_NewPlayer = False;
									MB_NewSpectator = True;
									+++OnNewPlayer+++
								}
							}
							
							+++PlayLoop+++						
						}
/* -------------------------------------- */
// Turn end
/* -------------------------------------- */
						if (MB_UseSectionTurn) {
							if (MB_UseLogging) MB_Log("EndTurn");
							+++EndTurn+++
						}
					}
/* -------------------------------------- */
// Round end
/* -------------------------------------- */
					if (MB_UseSectionRound) {
						if (MB_UseLogging) MB_Log("EndRound");
						+++EndRound+++
					}
				}
/* -------------------------------------- */
// Submatch end
/* -------------------------------------- */
				if (MB_UseSectionSubmatch) {
					if (MB_UseLogging) MB_Log("EndSubmatch");
					+++EndSubmatch+++
				}
			}
/* -------------------------------------- */
// Map end
/* -------------------------------------- */
			if (MB_UseLogging) MB_Log("EndMap");
			
			// Play mediatracker intro
			if (MB_UseOutro) {
				---MapOutro---
			}
			+++EndMap+++
			Mode::UnloadMap();
		}
/* -------------------------------------- */
// Match end
/* -------------------------------------- */
		if (MB_UseLogging) MB_Log("EndMatch");
		+++EndMatch+++
	}
/* -------------------------------------- */
// Server end
/* -------------------------------------- */
	if (MB_UseLogging) MB_Log("EndServer");
	+++EndServer+++
}